/************************************************************
'ca1' model code repository
Written by Marianne Bezaire, marianne.bezaire@gmail.com, www.mariannebezaire.com
In the lab of Ivan Soltesz, www.ivansolteszlab.org
Published and latest versions of this code are available online at:
ModelDB:
Open Source Brain: http://www.opensourcebrain.org/projects/nc_ca1

Main code file: ../main.hoc

This file defines a procedure related to brief pulses (shots) of
electrical current.
setCurrentShotAmplitude: this procedure sets the properties of the random number
   generators that are then associated to the noise functionality of
   the artificial cells. Each artificial cell has its own, unique
   random stream that is associated to its noise algorithm. The noise
   algorithm then draws from that random number stream to determine
   the interspike intervals that define the spike train pattern of the cell.
   However, only a subset of artificial cells are kept active;
   the other cells will be silenced throughout the simulation.
************************************************************/

proc setCurrentShotAmplitude() {local reli, typei, jgid, idx, slice, numslices localobj cell, r
	// This procedure takes three arguments:
	//  $1: SliceThickness: Thickness of the horizontal slice in microns (x-direction)
	//  $2: Slices: desired number of slices along the x-direction
	//  $3 = Percent of artificial neurons to be kept active

	if (int(LongitudinalLength/$1) >= $2) { // Desired circuit length is similar or smaller than the available x-length
	   targetCircuit_length = $1*$2
	} else {                                // Desired circuit length is larger than the available x-length
	   targetCircuit_length = $1*int(LongitudinalLength/$1)
    }
	numslices = int(targetCircuit_length/$1)

	r = new Random()
	r.uniform(0,1)
	for slice = 0, numslices-1 {
		for idx=0, numCellTypes-1 {
			if (cellType[idx].is_art==1) {	// If the cell type is artificial, set the random stream
											//  associated with the noise in its spike times

				// For all the artificial cells of this type, owned by this processor
				for pcitr(&reli, &typei, &jgid, cellType[idx].cellStartGid, cellType[idx].cellEndGid) {
					slice_index = cellType[idx].SliceIndList.x[jgid-cellType[idx].cellStartGid]
					if (pc.gid_exists(jgid) && (slice_index == slice)) {
						cell = pc.gid2cell(jgid)	// Create a reference to the artificial cell
						if (r.repick() < $3/100) {
							ranstimlist.object(reli).r.negexp(1) 	// Using a negative exponential distribution to determine
																	//  the interspike interval (ISI) gives Poisson-distributed
																	//  spike times. Always use 1 as the argument; the interval
																	//  for the netstim will set the 'rate' (or average ISI).
																	//
																	// For another example (which defines the randomstream class
																	//  differently, and therefore calls different commands at
																	//  different times) see the following file:
																	// <http://senselab.med.yale.edu/modeldb/showmodel.asp?model=83319&file=\destexhe_benchmarks\NEURON\common\netstim.hoc>
							cell.noiseFromRandom(ranstimlist.object(reli).r)	// Set this random stream as the random number
																				//  generator for the noise functionality of
																				//  the spike generator.
						} else {
							cell.start = -1 // This cell will be silent the whole simulation (unless it receives positive input from another cell)
						}
					}
				}
			}
		}
	}
}

setCurrentShotAmplitude(SliceThickness, Slices, PercentArtActive)
